(ns i18n.core-test
  (:require [clojure.test :refer [deftest is testing use-fixtures]]
            [i18n.core :as i18n]))

(use-fixtures :each (fn [t] (i18n/clear!) (t) (i18n/clear!)))

(deftest plain-string-lookup-test
  (i18n/register! :en {:app/title "Densha TODO"})
  (is (= "Densha TODO" (i18n/t :app/title))))

(deftest interpolation-test
  (i18n/register! :en {:auth/welcome "Welcome, {name}!"})
  (is (= "Welcome, Jun!" (i18n/t :auth/welcome {:name "Jun"})))
  (testing "string-keyed params also work (JSON interop)"
    (is (= "Welcome, Jun!" (i18n/t :auth/welcome {"name" "Jun"})))))

(deftest missing-key-degrades-visibly-test
  (is (= "{missing::app/title}" (i18n/t :app/title))))

(deftest locale-switch-and-fallback-test
  (i18n/register! :en {:app/title "Densha TODO" :auth/login "Login"})
  (i18n/register! :ja {:app/title "電車 TODO"})
  (i18n/set-locale! :ja)
  (is (= :ja (i18n/locale)))
  (is (= "電車 TODO" (i18n/t :app/title)))
  (testing "missing key in :ja catalog falls back to source (:en)"
    (is (= "Login" (i18n/t :auth/login)))))

(deftest set-source-locale-test
  (i18n/register! :ja {:app/title "電車 TODO"})
  (i18n/set-locale! :fr)
  (i18n/set-source-locale! :ja)
  (is (= "電車 TODO" (i18n/t :app/title))))

(deftest select-plural-test
  (i18n/register! :en {:todos/count {:select :count
                                     :one "{count} task"
                                     :other "{count} tasks"}})
  (is (= "1 task" (i18n/t :todos/count {:count 1})))
  (is (= "3 tasks" (i18n/t :todos/count {:count 3}))))

(deftest select-plural-arabic-test
  (i18n/register! :ar {:todos/count {:select :count
                                     :zero "لا مهام"
                                     :one "مهمة واحدة"
                                     :two "مهمتان"
                                     :few "{count} مهام"
                                     :many "{count} مهمة"
                                     :other "{count} مهمة"}})
  (i18n/set-locale! :ar)
  (is (= "لا مهام" (i18n/t :todos/count {:count 0})))
  (is (= "مهمتان" (i18n/t :todos/count {:count 2})))
  (is (= "5 مهام" (i18n/t :todos/count {:count 5}))))

(deftest register-merges-not-replaces-test
  (i18n/register! :en {:app/title "Densha TODO"})
  (i18n/register! :en {:auth/login "Login"})
  (is (= "Densha TODO" (i18n/t :app/title)))
  (is (= "Login" (i18n/t :auth/login))))
