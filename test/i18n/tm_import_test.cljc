(ns i18n.tm-import-test
  (:require [clojure.test :refer [deftest is testing]]
            [i18n.tm-import :as tm]))

(deftest export-messages->catalog-test
  (is (= {:app/title "Welcome" :auth/login "Login"}
         (tm/export-messages->catalog {"app.title" "Welcome" "auth.login" "Login"})))
  (testing "no dot -> no namespace"
    (is (= {:title "Welcome"} (tm/export-messages->catalog {"title" "Welcome"}))))
  (testing "splits on the LAST dot for deeper keys"
    (is (= {:todo.priority/low "Low"}
           (tm/export-messages->catalog {"todo.priority.low" "Low"})))))

(deftest catalog->register-project-test
  (is (= {"app.title" "Welcome"}
         (tm/catalog->register-project {:app/title "Welcome"})))
  (testing "select-map entries are dropped (TM service has no plural concept)"
    (is (= {} (tm/catalog->register-project
               {:todos/count {:select :count :one "1" :other "n"}})))))

(deftest round-trip-test
  (let [flat {"app.title" "Welcome" "auth.login" "Login"}]
    (is (= flat (tm/catalog->register-project (tm/export-messages->catalog flat))))))

(deftest language-registry->entries-test
  (let [body {:languages [{:code "ar" :name "Arabic" :dir "rtl"}
                          {:code "ja" :name "Japanese" :dir "ltr"}]}
        entries (tm/language-registry->entries body)]
    (is (= 2 (count entries)))
    (is (= {:i18n.lang/code :ar :i18n.lang/name "Arabic"
            :i18n.lang/native-name "Arabic" :i18n.lang/dir :rtl}
           (first entries)))
    (testing "string-keyed body (raw JSON) works too"
      (is (= entries (tm/language-registry->entries
                      {"languages" [{"code" "ar" "name" "Arabic" "dir" "rtl"}
                                    {"code" "ja" "name" "Japanese" "dir" "ltr"}]}))))))
